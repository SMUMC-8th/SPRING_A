plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.project'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Slf4j
    implementation 'org.slf4j:slf4j-api:2.0.7'

    // Lombok
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.7.0'

    // JPA
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")

    // MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // Jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    // AWS (S3)
    implementation platform('io.awspring.cloud:spring-cloud-aws-dependencies:3.3.0')
    implementation 'io.awspring.cloud:spring-cloud-aws-starter'
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // QueryDSL : OpenFeign
    implementation "io.github.openfeign.querydsl:querydsl-jpa:6.1"
    implementation "io.github.openfeign.querydsl:querydsl-core:6.1"
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:6.1:jpa"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"

    // WebClient
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // Netty
    implementation 'io.netty:netty-resolver-dns-native-macos:4.1.68.Final:osx-aarch_64'

    // OAuth2 login
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
}

jar{
    enabled=false
}

tasks.named('test') {
    useJUnitPlatform()
}

// QueryDSL 관련 설정
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

// 소스 세트에 생성 경로 추가 (구체적인 경로 지정)
sourceSets {
    main.java.srcDirs += [ querydslDir ]
}

// 컴파일 시 생성 경로 지정
tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory.set(querydslDir)
}

// clean 태스크에 생성 폴더 삭제 로직 추가
clean.doLast {
    file(querydslDir).deleteDir()
}
